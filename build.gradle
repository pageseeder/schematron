plugins {
  id 'application'
  id 'maven-publish'
  id 'io.codearte.nexus-staging' version '0.30.0'
}

group       = 'org.pageseeder.schematron'
version     = file('version.txt').text
description = "$title"

apply from: 'gradle/publish-mavencentral.gradle'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

tasks.withType(JavaCompile) {
  options.encoding = 'UTF-8'
}

repositories {
  mavenCentral {
    url = uri("https://maven-central.storage.googleapis.com/maven2")
  }
}

configurations {
  include
}

dependencies {

  include('name.dmaus.schxslt:schxslt:1.8.6')

  testImplementation('junit:junit:4.13.2')

  testRuntimeOnly('net.sf.saxon:Saxon-HE:9.9.1-6')

}

task includeTemplates(type: Copy) {
  from { configurations.include.collect { zipTree(it) } }
  exclude '/xproc/**'
  exclude '/META-INF/**'
  into 'src/main/resources/org/pageseeder/schematron/'
}

application {
  mainClassName = 'org.pageseeder.schematron.Main'
}